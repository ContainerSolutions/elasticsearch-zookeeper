import java.text.SimpleDateFormat

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'
version = '0.17.0'
elasticSearchVerstion = '0.17.7'
zooKeeperVersion = '3.3.3'
group = 'com.sonian'

buildTime = new Date()
SimpleDateFormat sdf = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss");
sdf.setTimeZone(TimeZone.getTimeZone("UTC"));
buildTimeStr = sdf.format(buildTime)

mavenRepoUser = System.getenv("REPO_USER")
mavenRepoPass = System.getenv("REPO_PASS")

test {
    useTestNG()
}

mavenRepoUrl = System.getenv("REPO_URL");
if (mavenRepoUrl == null) {
    mavenRepoUrl = "http://oss.sonatype.org/service/local/staging/deploy/maven2/"
}
mavenSnapshotRepoUrl = System.getenv("SNAPSHOT_REPO_URL");
if (mavenSnapshotRepoUrl == null) {
    mavenSnapshotRepoUrl = "http://oss.sonatype.org/content/repositories/snapshots"
}

configurations {
    deployerJars
    dist
}

repositories {
    mavenCentral()
    mavenRepo urls: 'https://repository.jboss.org/nexus/content/groups/public'
    mavenRepo urls: 'http://repository.codehaus.org/'
    mavenRepo urls: 'http://elasticsearch.googlecode.com/svn/maven'
    mavenRepo urls: 'http://oss.sonatype.org/content/repositories/releases'
    mavenRepo urls: 'http://oss.sonatype.org/content/repositories/snapshots'
    mavenRepo urls: 'http://download.java.net/maven/2/'
}

dependencies {
    compile group: 'org.elasticsearch', name: 'elasticsearch', version: elasticSearchVerstion
    compile group: 'org.apache.zookeeper', name: 'zookeeper', version: zooKeeperVersion, transitive: false

    testCompile group: 'org.testng', name: 'testng', version: '6.1.1'
    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3.RC2'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3.RC2'

    dist group: 'org.apache.zookeeper', name: 'zookeeper', version: zooKeeperVersion, ext: 'jar'

    runtime 'org.slf4j:slf4j-api:1.5.11'
    runtime 'org.slf4j:slf4j-log4j12:1.5.11'
    runtime 'log4j:log4j:1.2.16'
}

ideaProject {
    javaVersion = '1.6'
    version = '1.6'
}

task dist(type: Zip) {
    from jar.outputs.files
    from configurations.dist
    from('bin') {
        into 'bin'
        fileMode = 0755
    }
}

processResources.doLast {
    Properties versionProps = new Properties();
    versionProps.setProperty("number", rootProject.version)
    versionProps.setProperty("date", buildTimeStr)

    File versionFile = new File((File) sourceSets.main.classesDir, "/com/sonian/elasticsearch/plugin/zookeeper/version.properties")
    versionFile.parentFile.mkdirs()
    versionFile.withOutputStream {
        versionProps.store(it, '')
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        repository(url: mavenRepoUrl) {
            authentication(userName: mavenRepoUser, password: mavenRepoPass)
        }
        snapshotRepository(url: mavenSnapshotRepoUrl) {
            authentication(userName: mavenRepoUser, password: mavenRepoPass)
        }

        pom.project {
            inceptionYear '2011'
            name 'elasticsearch-zookeeper'
            description 'ZooKeeper plugin for elasticsearch'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'git://github.com/sonian/elasticsearch-zookeeper.git'
                developerConnection 'git@github.com:sonian/elasticsearch-zookeeper.git'
                url 'http://github.com/sonian/elasticsearch-zookeeper'
            }
        }
        pom.whenConfigured {pom ->
            pom.dependencies = pom.dependencies.findAll {dep -> dep.scope != 'test' } // removes the test scoped ones
            pom.dependencies = pom.dependencies.findAll {dep -> !dep.artifactId.contains('jna') } // remove jna, its optional
            pom.dependencies = pom.dependencies.findAll {dep -> !dep.artifactId.contains('jarjar') }
            pom.dependencies = pom.dependencies.findAll {dep -> !dep.artifactId.contains('log4j') }
            pom.dependencies = pom.dependencies.findAll {dep -> !dep.artifactId.contains('slf4j') }
            pom.dependencies = pom.dependencies.findAll {dep -> !dep.artifactId.contains('jline') }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.0-milestone-3'
}